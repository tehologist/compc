
: BOOT     0 ; 
: DPL      5 ; 
: '?KEY    8 ; 
: 'EMIT    9 ; 
: 'EXPECT 10 ; 
: 'TAP    11 ; 
: 'ECHO   12 ; 
: 'PROMPT 13 ; 
: BASE    14 ; 
: tmp     15 ; 
: SPAN    16 ; 
: >IN     17 ; 
: #TIBB   18 ; 
: TIBB    19 ; 
: CSP     20 ; 
: 'EVAL   21 ; 
: 'NUMBER 22 ; 
: HLD     23 ; 
: CURRENT 27 ; 
: CP      29 ; 
: NP      30 ; 
: LAST    31 ; 
: STATE   32 ; 

: TRUE -1 ;  
: FALSE 0 ; 

: BL 32 ; 
: BS 8 ; 

: =IMMED 3 ; 
: =WORDLIST 2 ; 

: + UM+ DROP ; 
: IMMEDIATE LAST @ -1 + =IMMED SWAP ! ; 

: +! SWAP OVER @ + SWAP ! ; 
: HERE CP @ ; 
: ALLOT CP +! ; 
: , HERE 1 ALLOT ! ;  
: [,] , ; IMMEDIATE  

: NOP      0 , ; IMMEDIATE 
: HALT     1 , ; IMMEDIATE 
: LIT      2 , ; IMMEDIATE 
: EXIT     3 , ; IMMEDIATE 
: @        4 , ; IMMEDIATE 
: !        5 , ; IMMEDIATE 
: DROP     6 , ; IMMEDIATE 
: OVER     7 , ; IMMEDIATE 
: SWAP     8 , ; IMMEDIATE 
: DUP      9 , ; IMMEDIATE 
: UM+     10 , ; IMMEDIATE 
: NAND    11 , ; IMMEDIATE 
: 0<      12 , ; IMMEDIATE 
: >R      13 , ; IMMEDIATE 
: R>      14 , ; IMMEDIATE 
: next    15 , ; IMMEDIATE 
: BRANCH  16 , ; IMMEDIATE 
: 0BRANCH 17 , ; IMMEDIATE 
: EXECUTE 18 , ; IMMEDIATE 
: PUTC    19 , ; IMMEDIATE 
: GETC    20 , ; IMMEDIATE 

: COMPILE R> DUP @ , 1 + >R ; 
: STATE? STATE @ ; 
: LITERAL COMPILE LIT , ; IMMEDIATE 

: [ FALSE STATE ! ; IMMEDIATE 
: ] TRUE STATE ! ; IMMEDIATE 

: IF COMPILE 0BRANCH HERE 0 , ; IMMEDIATE 
: THEN HERE SWAP ! ; IMMEDIATE 
: FOR COMPILE >R HERE ; IMMEDIATE 
: NEXT COMPILE next , ; IMMEDIATE 
: BEGIN HERE ; IMMEDIATE 
: AGAIN COMPILE BRANCH , ; IMMEDIATE 
: UNTIL COMPILE 0BRANCH , ; IMMEDIATE 
: AHEAD COMPILE BRANCH HERE 0 , ; IMMEDIATE 
: REPEAT ['] AGAIN [,] HERE SWAP ! ; IMMEDIATE 
: AFT DROP ['] AHEAD [,] ['] BEGIN [,] SWAP ; IMMEDIATE 
: ELSE ['] AHEAD [,] SWAP ['] THEN [,] ; IMMEDIATE 
: WHILE ['] IF [,] SWAP ; IMMEDIATE 
: @EXECUTE @ DUP IF EXECUTE THEN ; 

: @       STATE? IF 
              COMPILE @ EXIT THEN @ ; IMMEDIATE 
: !       STATE? IF 
              COMPILE ! EXIT THEN ! ; IMMEDIATE 
: DROP    STATE? IF 
              COMPILE DROP EXIT THEN DROP ; IMMEDIATE 
: OVER    STATE? IF 
              COMPILE OVER EXIT THEN OVER ; IMMEDIATE 
: SWAP    STATE? IF 
              COMPILE SWAP EXIT THEN SWAP ; IMMEDIATE 
: DUP     STATE? IF 
              COMPILE DUP EXIT THEN DUP ; IMMEDIATE 

: #TIB #TIBB @ ; 
: TIB TIBB @ ;  
: \ #TIB @ >IN ! ; IMMEDIATE 

100 #TIBB ! 
101 TIBB ! 

: ROT >R SWAP R> SWAP ; 
: -ROT SWAP >R SWAP R> ; 
: NIP SWAP DROP ; 
: 2DROP DROP DROP ; 
: 2DUP OVER OVER ; 
: R@ R> R> DUP >R SWAP >R ; 

: NOT DUP NAND ; 
: AND NAND NOT ; 
: OR NOT SWAP NOT NAND ; 
: NOR OR NOT ; 
: XOR 2DUP AND -ROT NOR NOR ; 
: XNOR XOR NOT ; 
: NEGATE NOT 1 + ; 
: - NEGATE + ; 

: D+ >R SWAP >R UM+ R> R> + + ; 
: DNEGATE NOT >R NOT 1 UM+ R> + ; 
: D- DNEGATE D+ ; 

: 1+ 1 + ; 
: 1- 1 - ; 
: 2+ 2 + ; 
: 2- 2 - ; 

: CELL+   1 + ; 
: CELL-   1 - ; 
: CELLS   0 + ; 
: ALIGNED 1 + ; 

: U< 2DUP XOR 0< IF SWAP DROP 0< EXIT THEN - 0< ; 
: U> SWAP U< ; 
: = XOR IF FALSE EXIT THEN TRUE ; 
: < 2DUP XOR 0< IF DROP 0< EXIT THEN - 0< ; 
: > SWAP < ; 
: 0> NEGATE 0< ; 
: 0<> IF TRUE EXIT THEN FALSE ; 
: 0= 0 = ; 
: <> = 0= ; 

: MIN 2DUP SWAP < IF SWAP THEN DROP ; 
: MAX 2DUP < IF SWAP THEN DROP ; 

: ?DUP DUP IF DUP THEN ; 
: S>D DUP 0< ; 
: ABS DUP 0< IF NEGATE THEN ; 
: DABS DUP 0< IF DNEGATE THEN ; 

: UM/MOD 
    2DUP U< 
    IF NEGATE 
        15 FOR 
            >R DUP UM+ 
                >R >R DUP UM+ 
                R> + DUP R> R@ SWAP 
            >R UM+ 
            R> OR 
                IF >R DROP 1+ R> 
                ELSE DROP 
            THEN R> 
        NEXT DROP SWAP EXIT 
    THEN DROP 2DROP -1 DUP ; 
    
: UM* 
    0 SWAP 
    15 FOR 
        DUP UM+ >R >R 
        DUP UM+ 
        R> + 
        R> 
        IF >R OVER UM+ 
            R> + 
        THEN 
    NEXT 
    ROT DROP ; 

: parse 
    tmp ! OVER >R DUP 
    IF 
        1 - tmp @ BL = 
        IF 
            FOR BL OVER @ - 0< NOT 
            WHILE 1+ 
            NEXT R> DROP 0 DUP EXIT 
            THEN R> 
        THEN 
            OVER SWAP 
        FOR tmp @ OVER @ - tmp @ BL = 
            IF 0< THEN 
        WHILE 1+ 
        NEXT DUP >R 
        ELSE R> DROP DUP 1+ >R 
        THEN OVER - R> R> - EXIT 
    THEN OVER R> - ; 
: PARSE >R TIB >IN @ + #TIB @ >IN @ - R> parse >IN +! ; 
: CHAR BL PARSE DROP @ ; 

: CMOVE 
    FOR AFT 
        >R 
            DUP @ R@ ! 1+ 
        R> 1+ 
    THEN NEXT 
    2DROP ; 
    
: PACK$ 
    DUP 
    >R 
        2DUP ! 1+ 2DUP + 0 SWAP ! SWAP CMOVE 
    R> ; 
: WORD PARSE HERE PACK$ ; 
: TOKEN BL PARSE 31 MIN NP @ OVER - 1 - PACK$ ; 
: NAME> 2 CELLS - @ ; 
: LINK> 3 CELLS - ; 
: SAME? 
    FOR AFT 
        OVER R@ CELLS + @ 
        OVER R@ CELLS + @ 
        - ?DUP 
        IF R> DROP EXIT THEN 
    THEN 
    NEXT 0 ; 
: find 
    SWAP  
    DUP @ tmp ! 
    DUP @ >R 
    1+ SWAP   
    BEGIN 
        @ DUP 
        IF DUP @ R@ XOR 
            IF 1+ -1 
            ELSE 1+ tmp @ SAME? 
            THEN 
        ELSE 
            R> DROP 
            SWAP 1- SWAP 
            EXIT 
        THEN 
    WHILE 4 - 
    REPEAT 
    R> DROP NIP 1- 
    DUP NAME> 
    SWAP ; 
: ' TOKEN CURRENT find DUP IF DROP ELSE SWAP DROP THEN ; 
: ['] COMPILE ' ; IMMEDIATE 
: [COMPILE] ' , ; IMMEDIATE 
: [CHAR] CHAR [COMPILE] LITERAL ; IMMEDIATE 
: ( [CHAR] ) PARSE 2DROP ; IMMEDIATE 
: OVERT LAST @ CURRENT ! ; 
: $,n 
    DUP LAST ! 1- 
    DUP =WORDLIST 
    SWAP ! 
    1- DUP HERE 
    SWAP ! 
    1- DUP CURRENT @ 
    SWAP ! 
    1- NP ! ; 
: doVAR R> ; 
: CREATE TOKEN $,n COMPILE doVAR OVERT ; 
: VARIABLE CREATE 0 , ; 
: : TOKEN $,n [COMPILE] ] ; 
: ; COMPILE EXIT [COMPILE] [  OVERT ; IMMEDIATE 

: TX! PUTC ; 
: EMIT 'EMIT @EXECUTE ; 
: COUNT DUP 1+ SWAP @ ; 
: TYPE FOR AFT DUP @ EMIT 1+ THEN NEXT DROP ; 
' TX! 'EMIT ! 

: ?RX GETC ; 
: ?KEY '?KEY @EXECUTE ; 
: KEY BEGIN ?KEY UNTIL ; 
' ?RX '?KEY !  

: SPACE BL EMIT ; 
: SPACES 0 MAX FOR SPACE NEXT ; 
: PAD HERE 80 + ;  
: DECIMAL 10 BASE ! ; 
: HEX 16 BASE ! ; 

DECIMAL 
: DIGIT 9 OVER < 7 AND + [CHAR] 0 + ; 
: <# PAD HLD ! ; 
: HOLD HLD @ 1- DUP HLD ! ! ; 
: # 0 BASE @ UM/MOD >R BASE @ UM/MOD SWAP DIGIT HOLD R> ; 
: #S BEGIN # 2DUP OR 0= UNTIL ; 
: SIGN 0< IF [ CHAR - ] LITERAL HOLD THEN ; 
: #> 2DROP HLD @ PAD OVER - ; 
: S.R OVER - SPACES TYPE ; 
: D.R >R DUP >R DABS <# #S R> SIGN #> R> S.R ; 
: U.R 0 SWAP D.R ; 
: .R >R S>D R> D.R ; 
: D. 0 D.R SPACE ; 
: U. 0 D. ;  
: . BASE @ 10 XOR IF U. EXIT THEN S>D D. ; 
: ? @ . ; 

: do$ R> R@ R> COUNT + 1+ >R SWAP >R ; 
: ."| do$ COUNT TYPE ; 
: $," [CHAR] " WORD COUNT + 1+ CP ! ; 
: ." COMPILE ."| $," ; IMMEDIATE 
: .( [CHAR] ) PARSE TYPE ; IMMEDIATE 
: $"| do$ ; 
: $" COMPILE $"| $," ; IMMEDIATE 
: s" [CHAR] " PARSE PAD PACK$ ; 

: DIGIT? 
    >R 
    [CHAR] 0 - 
    9 OVER < 
    IF 7 - DUP 10 < OR THEN 
    DUP R> U< ; 
: /STRING DUP >R - SWAP R> + SWAP ; 

: >NUMBER 
    BEGIN DUP 
    WHILE >R DUP >R @ BASE @ DIGIT? 
    WHILE SWAP BASE @ UM* DROP 
    ROT BASE @ UM* D+ R> 1+ R> 1- 
    REPEAT DROP R> R> THEN ; 

: NUMBER? 
    OVER @ [CHAR] - = DUP >R IF 1 /STRING THEN 
    >R >R 0 DUP R> R> -1 DPL ! 
    BEGIN >NUMBER DUP 
    WHILE OVER @ [CHAR] . XOR 
        IF ROT DROP ROT R> 2DROP 0 EXIT 
        THEN 1 - DPL ! 1+ DPL @ 
    REPEAT 2DROP R> IF DNEGATE THEN -1 ; 
' NUMBER? 'NUMBER ! 

: $INTERPRET 
    CURRENT find ?DUP 
    IF DROP EXECUTE EXIT THEN 
    COUNT 'NUMBER @EXECUTE IF 
        DPL @ 0<  IF DROP THEN EXIT THEN ." ? " TYPE ; 
: $COMPILE 
    CURRENT find ?DUP 
    IF 1- @ =IMMED = 
    IF EXECUTE ELSE , THEN EXIT 
    THEN COUNT 'NUMBER @EXECUTE 
    IF 
        DPL @ 0< 
        IF DROP [COMPILE] LITERAL
        ELSE SWAP [COMPILE] LITERAL [COMPILE] LITERAL 
        THEN EXIT 
    THEN ." ? " TYPE ; 
: eval STATE? IF $COMPILE ELSE $INTERPRET THEN ; 
' eval 'EVAL ! 
: EVAL 
    BEGIN TOKEN DUP @ WHILE 
        'EVAL @EXECUTE  
    REPEAT DROP ; 

: CR 10 EMIT ;  
' DROP 'ECHO ! 
: ^H 
    >R OVER R> 
    SWAP OVER XOR 
    IF 
        BS EMIT 1+ 
        BL EMIT 
        BS EMIT 
    THEN ; 
: TAP DUP 'ECHO @EXECUTE OVER ! 1+ ; 
: KTAP 
    DUP 10 XOR 
    IF BS XOR 
        IF BL TAP 
        ELSE ^H 
        THEN EXIT 
    THEN 
    DROP NIP DUP ; 
' KTAP 'TAP ! 
: ACCEPT 
    OVER + OVER 
    BEGIN 
        2DUP XOR 
    WHILE 
        KEY 
        DUP BL - 95 U< 
        IF TAP 
        ELSE 'TAP @EXECUTE 
        THEN 
    REPEAT 
    DROP 
    OVER - ; 
' ACCEPT 'EXPECT ! 
: QUERY TIB 80 'EXPECT @EXECUTE #TIB ! DROP 0 >IN ! ; 
: BYE ." See Ya " HALT ; 
: OK CR ." OK. " ; 
' OK 'PROMPT ! 
: QUIT 
    BEGIN 'PROMPT @EXECUTE QUERY EVAL AGAIN ; 
' QUIT BOOT ! 
